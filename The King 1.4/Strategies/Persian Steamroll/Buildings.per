;Buildings

	;Economic Buildings

        ;Town Centers

		(defrule
			(building-type-count-total town-center < 1)
            (can-build-with-escrow town-center)
		=>
            (release-escrow wood)
            (release-escrow stone)
			(build town-center)
		)

        (defrule
			(building-type-count-total town-center < 3)
            (building-type-count-total castle >= 1)
            (can-build-with-escrow town-center)
		=>
            (release-escrow wood)
            (release-escrow stone)
			(build town-center)
		)

        ;Houses

        (defrule
            (building-type-count-total town-center >= 1)
            (housing-headroom < 4)
            (population-headroom > 3)
            (can-build house)
        =>
            (build house)
        )

        (defrule
            (building-type-count-total town-center >= 1)
            (current-age >= feudal-age)
            (housing-headroom < 10)
            (population-headroom > 3)
            (can-build house)
        =>
            (build house)
        )

        ;Lumber camps
        
        (defrule
            (building-type-count-total lumber-camp == 0)
			(unit-type-count villager > 6)
			(resource-found wood)
			(can-build lumber-camp)
		=>
			(build lumber-camp)
		)

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp < 2)
            (building-type-count-total market >= 1)
			(current-age >= feudal-age)
			(can-build lumber-camp)
		=>
			(build lumber-camp)
		)

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp < 4)
            (building-type-count-total monastery >= 1)
			(current-age >= castle-age)
			(can-build lumber-camp)
		=>
			(build lumber-camp)
		)

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp < 6)
			(current-age >= imperial-age)
			(can-build lumber-camp)
		=>
			(build lumber-camp)
		)

        ;Mining camps

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp >= 1)
            (building-type-count-total mining-camp == 0)
            (unit-type-count villager > 22)
			(resource-found gold)
            (can-build mining-camp)
		=>
			(build mining-camp)
            (chat-to-all "Building mining camp")
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total mining-camp < 2)
            (unit-type-count villager > 24)
			(resource-found stone)
            (can-build mining-camp)
		=>
			(build mining-camp)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total mining-camp < 3)
            (stone-amount == 200)
            (unit-type-count villager > 26)
			(resource-found stone)
            (can-build mining-camp)
		=>
			(build mining-camp)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total siege-workshop >= 1)
            (building-type-count-total mining-camp < 4)
            (current-age >= castle-age)
            (can-build mining-camp)
		=>
			(build mining-camp)
		)

        ;Farms

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp >= 1)
            (unit-type-count 594 <= 1)
            (idle-farm-count < 2)
            (current-age == dark-age)
            (unit-type-count villager <= 23)
            (can-build farm)
		=>
			(build farm)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (unit-type-count 594 <= 1)
            (idle-farm-count < 2)
            (current-age == dark-age)
            (building-type-count-total mining-camp >= 2)
            (unit-type-count villager > 24)
            (can-build farm)
		=>
			(build farm)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total market >= 1)
            (idle-farm-count < 2)
            (current-age == feudal-age)
            (can-build farm)
		=>
			(build farm)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total monastery >= 1)
            (idle-farm-count < 2)
            (current-age >= castle-age)
            (can-build farm)
		=>
			(build farm)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (idle-farm-count < 2)
            (game-time > 2400)
            (can-build farm)
		=>
			(build farm)
		)

        ;Blacksmiths

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total blacksmith < 1)
            (can-build blacksmith)
        =>
            (build blacksmith)
            (chat-to-all "Building blacksmith")
        )

        ;Markets

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total blacksmith >= 1)
            (building-type-count-total market < 1)
            (can-build market)
        =>
            (build market)
            (chat-to-all "Building market")
        )

        ;Universities

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total siege-workshop >= 1)
            (building-type-count-total university < 1)
            (can-build university)
        =>
            (build university)
            (chat-to-all "Building university")
        )

        (defrule
            (building-type-count-total town-center >= 1)
            (or (goal UNIQUE-UNIT-CLASS ARCHER) (goal UNIQUE-UNIT-CLASS CAV-ARCHER))
            (building-type-count-total university < 1)
            (can-build university)
        =>
            (build university)
            (chat-to-all "Building university")
        )

    ;Military Buildings

        ;Barracks

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total barracks < 1)
            (building-type-count-total monastery >= 1)
            (goal TO-CASTLE YES)
            (can-build barracks)
        =>
            (build barracks)
        )

        ;Stable

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total stable < 1)
            (building-type-count-total monastery >= 1)
            (goal TO-CASTLE YES)
            (can-build stable)
        =>
            (build stable)
        )

        ;Siege Workshop

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total monastery >= 1)
            (building-type-count-total siege-workshop < 1)
            (can-build siege-workshop)
        =>
            (build siege-workshop)
            (chat-to-all "Building siege workshop")
        )

        ;Monastery

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total monastery < 1)
            (can-build monastery)
        =>
            (build monastery)
            (chat-to-all "Building monastery")
        )

        ;Castle

        (defrule
            (can-build castle)
        =>
            (build castle)
        )

        ;Walls

        (defrule
            (current-age == castle-age)
            (building-type-count-total castle >= 1)
        =>
            (set-goal WALLS YES)
        )
        

        (defrule
            (goal WALLS YES)
            (current-age >= castle-age)
        =>
            (enable-wall-placement 2)
            (disable-self)
        )

        (defrule
            (goal WALLS YES)
            (current-age >= castle-age)
            (can-build-wall 2 stone-wall-line)
            (not (town-under-attack))
        =>
            (build-wall 2 stone-wall-line)
        )

        (defrule
            (goal WALLS YES)
            (current-age >= castle-age)
            (can-build-gate 2)
            (not (town-under-attack))
        =>
            (build-gate 2)
        )