;Buildings

	;Economic Buildings

        ;Town Centers

		(defrule
			(building-type-count-total town-center < 1)
            (can-build-with-escrow town-center)
		=>
            (release-escrow wood)
            (release-escrow stone)
			(build town-center)
		)

        ;Houses

        (defrule
            (building-type-count-total town-center >= 1)
            (housing-headroom < 4)
            (population-headroom > 3)
            (can-build house)
        =>
            (build house)
        )

        (defrule
            (building-type-count-total town-center >= 1)
            (current-age >= feudal-age)
            (housing-headroom < 10)
            (population-headroom > 3)
            (can-build house)
        =>
            (build house)
        )

        ;Lumber camps
        
        (defrule
            (building-type-count-total lumber-camp == 0)
			(unit-type-count villager > 6)
			(resource-found wood)
			(can-build lumber-camp)
		=>
			(build lumber-camp)
		)

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp < 2)
            (building-type-count-total market >= 1)
			(goal TO-IMPERIAL YES)
			(can-build lumber-camp)
		=>
			(build lumber-camp)
		)

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp < 6)
			(current-age >= imperial-age)
			(can-build lumber-camp)
		=>
			(build lumber-camp)
		)

        ;Mills

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total mill == 0)
            (unit-type-count villager > 6)
            (or (unit-type-count 594 < 2) (building-type-count-total lumber-camp >= 1))
			(resource-found food)
            (can-build mill)
		=>
			(build mill)
		)

        ;Mining camps

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp >= 1)
            (building-type-count-total mining-camp == 0)
            (unit-type-count villager > 22)
			(resource-found gold)
            (can-build mining-camp)
		=>
			(build mining-camp)
            (chat-to-all "Building mining camp")
		)

        (defrule
			(goal TO-CASTLE YES)
            (building-type-count-total mining-camp < 2)
            (can-build mining-camp)
		=>
			(build mining-camp)
            (chat-to-all "Building mining camp")
		)

        ;Farms

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp >= 1)
            (unit-type-count 594 <= 1)
            (idle-farm-count < 2)
            (current-age == dark-age)
            (unit-type-count villager <= 23)
            (can-build farm)
		=>
			(build farm)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (unit-type-count 594 <= 1)
            (idle-farm-count < 2)
            (current-age == dark-age)
            (building-type-count-total mining-camp >= 2)
            (unit-type-count villager > 24)
            (can-build farm)
		=>
			(build farm)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total market >= 1)
            (idle-farm-count < 2)
            (current-age == feudal-age)
            (can-build farm)
		=>
			(build farm)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (building-type-count-total monastery >= 1)
            (idle-farm-count < 2)
            (current-age >= castle-age)
            (can-build farm)
		=>
			(build farm)
		)

        (defrule
			(building-type-count-total town-center >= 1)
            (idle-farm-count < 2)
            (game-time > 2400)
            (can-build farm)
		=>
			(build farm)
		)

        ;Blacksmiths

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total blacksmith < 1)
            (can-build blacksmith)
        =>
            (build blacksmith)
            (chat-to-all "Building blacksmith")
        )

        ;Markets

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total blacksmith >= 1)
            (building-type-count-total market < 1)
            (can-build market)
        =>
            (build market)
            (chat-to-all "Building market")
        )

        ;Universities

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total monastery >= 2)
            (building-type-count-total university < 1)
            (current-age == imperial-age)
            (can-build university)
        =>
            (build university)
            (chat-to-all "Building university")
        )

    ;Military Buildings

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total barracks < 1)
            (building-type-count-total monastery >= 1)
            (goal TO-IMPERIAL YES)
            (can-build barracks)
        =>
            (build barracks)
        )

        ;Stable

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total archery-range >= 2)
            (building-type-count-total stable < 2)
            (unit-type-count-total bombard-cannon >= 1)
            (goal TO-IMPERIAL YES)
            (can-build stable)
        =>
            (build stable)
        )

        ;Archery Range

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total archery-range < 2)
            (goal TO-IMPERIAL YES)
            (can-build archery-range)
        =>
            (build archery-range)
        )

        ;Siege Workshop

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total monastery >= 1)
            (building-type-count-total siege-workshop < 1)
            (can-build siege-workshop)
        =>
            (build siege-workshop)
            (chat-to-all "Building siege workshop")
        )

        ;Monastery

        (defrule
            (building-type-count-total town-center >= 1)
            (building-type-count-total monastery < 1)
            (can-build monastery)
        =>
            (build monastery)
            (chat-to-all "Building monastery")
        )

        ;Castle

        (defrule
            (can-build castle)
        =>
            (build castle)
        )