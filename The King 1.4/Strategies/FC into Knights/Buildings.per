;Buildings

	;Economic Buildings

        ;Lumber camps
        
        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp < 2)
            (building-type-count-total market >= 1)
			(current-age >= feudal-age)
			(can-build lumber-camp)
		=>
			(set-goal NEXT-BUILDING lumber-camp)
            (chat-to-player my-player-number "Want to build: Lumber camp")
		)

        ;Mining camps

        (defrule
            (goal NEXT-BUILDING 0)
			(building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp >= 1)
            (building-type-count-total mining-camp == 0)
            (unit-type-count villager > 22)
			(resource-found gold)
		=>
			(set-goal NEXT-BUILDING mining-camp)
            (chat-to-player my-player-number "Want to build: Mining camp")
		)

        (defrule
            (goal NEXT-BUILDING 0)
			(goal TO-CASTLE YES)
            (building-type-count-total mining-camp < 2)
            (can-build mining-camp)
		=>
			(set-goal NEXT-BUILDING mining-camp)
            (chat-to-player my-player-number "Want to build: Mining camp")
		)

        ;Farms

        (defrule
            (goal NEXT-BUILDING 0)
			(building-type-count-total town-center >= 1)
            (building-type-count-total lumber-camp >= 1)
            (unit-type-count 594 <= 1)
            (idle-farm-count < 2)
            (current-age == dark-age)
            (unit-type-count villager <= 23)
            (can-build farm)
		=>
			(set-goal NEXT-BUILDING farm)
            (chat-to-player my-player-number "Want to build: Farm")
		)

        (defrule
            (goal NEXT-BUILDING 0)
			(building-type-count-total town-center >= 1)
            (unit-type-count 594 <= 1)
            (idle-farm-count < 2)
            (current-age == dark-age)
            (wood-amount > 350)
            (unit-type-count villager > 24)
            (can-build farm)
		=>
			(set-goal NEXT-BUILDING farm)
            (chat-to-player my-player-number "Want to build: Farm")
		)

        (defrule
            (goal NEXT-BUILDING 0)
			(building-type-count-total town-center >= 1)
            (or (building-type-count-total market >= 1) (wood-amount > 350))
            (goal TO-CASTLE NO)
            (idle-farm-count < 2)
            (current-age == feudal-age)
            (can-build farm)
		=>
			(set-goal NEXT-BUILDING farm)
            (chat-to-player my-player-number "Want to build: Farm")
		)

        (defrule
            (goal NEXT-BUILDING 0)
			(building-type-count-total town-center >= 1)
            (building-type-count-total stable >= 1)
            (goal TO-CASTLE YES)
            (idle-farm-count < 2)
            (current-age == feudal-age)
            (can-build farm)
		=>
			(set-goal NEXT-BUILDING farm)
            (chat-to-player my-player-number "Want to build: Farm")
		)

        (defrule
            (wood-amount > 300)
            (not (goal NEXT-BUILDING farm))
			(building-type-count-total town-center >= 1)
            (building-type-count-total stable >= 1)
            (goal TO-CASTLE YES)
            (idle-farm-count < 2)
            (current-age == feudal-age)
            (can-build farm)
		=>
			(set-goal NEXT-BUILDING farm)
            (chat-to-player my-player-number "Want to build: Farm")
		)

        (defrule
            (goal NEXT-BUILDING 0)
			(building-type-count-total town-center >= 1)
            (building-type-count-total monastery >= 1)
            (idle-farm-count < 2)
            (current-age >= castle-age)
            (can-build farm)
		=>
			(set-goal NEXT-BUILDING farm)
            (chat-to-player my-player-number "Want to build: Farm")
		)

        (defrule
            (wood-amount > 300)
            (not (goal NEXT-BUILDING farm))
			(building-type-count-total town-center >= 1)
            (building-type-count-total monastery >= 1)
            (idle-farm-count < 2)
            (current-age >= castle-age)
            (can-build farm)
		=>
			(set-goal NEXT-BUILDING farm)
            (chat-to-player my-player-number "Want to build: Farm")
		)

        (defrule
            (goal NEXT-BUILDING 0)
			(building-type-count-total town-center >= 1)
            (idle-farm-count < 2)
            (game-time > 2400)
            (can-build farm)
		=>
			(set-goal NEXT-BUILDING farm)
            (chat-to-player my-player-number "Want to build: Farm")
		)

        ;Blacksmiths

        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (current-age >= feudal-age)
            (building-type-count-total blacksmith < 1)
        =>
            (set-goal NEXT-BUILDING blacksmith)
            (chat-to-player my-player-number "Want to build: Blacksmith")
        )

        ;Markets

        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (current-age >= feudal-age)
            (building-type-count-total market < 1)
        =>
            (set-goal NEXT-BUILDING market)
            (chat-to-player my-player-number "Want to build: Market")
        )

        ;Universities

        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (building-type-count-total siege-workshop >= 1)
            (building-type-count-total university < 1)
            (current-age >= castle-age)
            (can-build university)
        =>
            (set-goal NEXT-BUILDING university)
            (chat-to-player my-player-number "Want to build: University")
        )

    ;Military Buildings

        ;Barracks

        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (building-type-count-total barracks < 1)
            (goal TO-CASTLE YES)
        =>
            (set-goal NEXT-BUILDING barracks)
            (chat-to-player my-player-number "Want to build: Barracks")
        )

        ;Stable

        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (building-type-count-total stable < 1)
            (building-type-count-total barracks >= 1)
            (goal TO-CASTLE YES)
        =>
            (set-goal NEXT-BUILDING stable)
            (chat-to-player my-player-number "Want to build: Stable")
        )

        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (building-type-count-total siege-workshop >= 1)
            (building-type-count-total stable < 3)
            (current-age >= castle-age)
        =>
            (set-goal NEXT-BUILDING stable)
            (chat-to-player my-player-number "Want to build: Stable")
        )

        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (building-type-count-total siege-workshop >= 1)
            (building-type-count-total stable < 5)
            (current-age >= imperial-age)
            (can-build stable)
        =>
            (set-goal NEXT-BUILDING stable)
            (chat-to-player my-player-number "Want to build: Stable")
        )

        ;Siege Workshop

        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (building-type-count-total siege-workshop < 1)
            (building-type-count-total stable >= 1)
        =>
            (set-goal NEXT-BUILDING siege-workshop)
            (chat-to-player my-player-number "Want to build: Siege workshop")
        )

        ;Monastery

        (defrule
            (goal NEXT-BUILDING 0)
            (building-type-count-total town-center >= 1)
            (building-type-count-total monastery < 1)
            (building-type-count-total stable >= 1)
        =>
            (set-goal NEXT-BUILDING monastery)
            (chat-to-player my-player-number "Want to build: Monastery")
        )

        ;Castle

        (defrule
            (goal NEXT-BUILDING 0)
            (can-build castle)
        =>
            (set-goal NEXT-BUILDING castle)
            (chat-to-player my-player-number "Want to build: Castle")
        )